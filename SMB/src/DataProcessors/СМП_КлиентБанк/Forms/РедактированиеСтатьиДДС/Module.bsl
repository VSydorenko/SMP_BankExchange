#Область ОписаниеПеременных

&НаКлиенте
Перем КэшСтатьиРасходовНаОС; // чтобы по нескольку раз не обращаться к общему модулю за статьёй расходов. 

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Множественный Тогда
		СтатьяДДСВходящие   = Параметры.СтатьяДДСВходящих;
		СтатьяДДСИсходящие  = Параметры.СтатьяДДСИсходящих;
	Иначе
		СтатьяДДСВходящие   = Параметры.СтатьяДДС;
		СтатьяДДСИсходящие  = Параметры.СтатьяДДС;
	КонецЕсли;
	
	НазначениеПлатежа     = Параметры.НазначениеПлатежа;
	Исходящий             = Параметры.Исходящий;
	Множественный         = Параметры.Множественный;
	КоличествоВходящих    = Параметры.КоличествоВходящих;
	КоличествоИсходящих   = Параметры.КоличествоИсходящих;
	Если Параметры.Множественный Тогда
		Элементы.НазначениеПлатежа.Видимость = Ложь;
		
		Элементы.СтатьяДДСВходящие.Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Статья поступлений (%1 %2)'"),
				КоличествоВходящих,
				ПодобратьСловоПодЧисло(
					КоличествоВходящих,
					НСтр("ru='документ'"),
					НСтр("ru='документа'"),
					НСтр("ru='документов'")));
			
		Элементы.СтатьяДДСИсходящие.Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Статья списаний (%1 %2)'"),
				КоличествоИсходящих,
				ПодобратьСловоПодЧисло(
					КоличествоИсходящих,
					НСтр("ru='документ'"),
					НСтр("ru='документа'"),
					НСтр("ru='документов'")));
		
	Иначе
		Элементы.СтатьяДДСВходящие.Видимость   = НЕ Параметры.Исходящий;
		Элементы.СтатьяДДСИсходящие.Видимость  = Параметры.Исходящий;
	КонецЕсли;
	
	УстановитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Очистить();
	
	Если КоличествоИсходящих > 0 ИЛИ Исходящий Тогда
		ПроверяемыеРеквизиты.Добавить("СтатьяДДСИсходящие");
	КонецЕсли;
	
	Если КоличествоВходящих > 0 ИЛИ (НЕ Исходящий И НЕ Множественный) Тогда
		ПроверяемыеРеквизиты.Добавить("СтатьяДДСВходящие");
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(
		Новый Структура("СтатьяДДС, СтатьяДДСИсходящих, СтатьяДДСВходящих", 
			?(Исходящий,СтатьяДДСИсходящие, СтатьяДДСВходящие),
			СтатьяДДСИсходящие,
			СтатьяДДСВходящие));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть(КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ И ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьВидимостьИДоступностьЭлементов()
	
	Если Множественный Тогда
		Элементы.ГруппаНастройкиВходящих.Видимость = (КоличествоВходящих>0);
		Элементы.ГруппаНастройкиИсходящих.Видимость = (КоличествоИсходящих>0);
	Иначе
		Если Исходящий Тогда
			Элементы.ГруппаНастройкиИсходящих.Видимость = Истина;
			Элементы.ГруппаНастройкиВходящих.Видимость = Ложь;
		Иначе
			Элементы.ГруппаНастройкиИсходящих.Видимость = Ложь;
			Элементы.ГруппаНастройкиВходящих.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подбирает слова под число
// Например:
//		ПодобратьСловоПодЧисло(21, "Дань", "Дня", "Дней") = День
//		ПодобратьСловоПодЧисло(35, "Дань", "Дня", "Дней") = Дней
//
Функция ПодобратьСловоПодЧисло(Число, СловоДляОдин, СловоДляДва, СловоДляПять) Экспорт
	
	Если Число > 4 И Число < 21 Тогда
		
		Возврат СловоДляПять;
		
	ИначеЕсли (Число % 10) = 1 Тогда
		
		Возврат СловоДляОдин;
		
	ИначеЕсли (Число % 10) <= 5 Тогда
		
		Возврат СловоДляДва;
		
	Иначе
		
		Возврат СловоДляПять;
		
	КонецЕсли;
	
КонецФункции
