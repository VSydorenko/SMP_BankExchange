
&НаКлиенте
Процедура СМП_СписокБанковскиеВыпискиПриАктивизацииСтрокиПосле(Элемент)
	
	УстановитьДоступностьЗагрузитьИзФайла();
	
КонецПроцедуры

// доступность кнопки загрузки КБ.
//
// Параметры:
//   Нет.
//
&НаКлиенте
Процедура УстановитьДоступностьЗагрузитьИзФайла() 
	
	ТекДанные = Элементы.СписокБанковскиеВыписки.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ЗагрузитьИзФайла.Доступность = (ТипЗнч(ТекДанные.СчетКасса) = Тип("СправочникСсылка.БанковскиеСчета"));
	
КонецПроцедуры //УстановитьДоступностьЗагрузитьИзФайла

&НаКлиенте
Процедура СМП_ЗагрузитьИзФайлаВместо(Команда)
	
	БанкСчет = Неопределено;
	ТекДанные = Элементы.СписокБанковскиеВыписки.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		БанкСчет = ТекДанные.БанковскийСчет;
	Иначе
		
		мОтбор = Новый Структура("ИмяГруппыРодителя, ИмяПараметраЗапроса, ИмяПоляОтбора, ИмяСписка", 
		"ГруппаОтборСчетИКасса", "СписокКассИСчетов", "СчетКассаДляОтбора", "СписокБанковскиеВыписки");
		СтрокиОтбора = ДанныеМетокБанковскиеВыписки.НайтиСтроки(мОтбор);
		Для Каждого мСтрока Из СтрокиОтбора Цикл
			Если ТипЗнч(мСтрока.Метка) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
				БанкСчет = мСтрока.Метка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ЗагрузитьИзФайлаФрагмент(БанкСчет);
	
КонецПроцедуры

&НаКлиенте
&Вместо("ЗагрузитьИзФайлаФрагмент")
Процедура СМП_ЗагрузитьИзФайлаФрагмент(ПараметрБанковскийСчет)
	
	СтруктураОбмена = КлиентБанкВызовСервера.ПолучитьСтруктуруОбмена(ПараметрБанковскийСчет);
	ОткрытьФорму("Обработка.СМП_КлиентБанк.Форма.ФормаЗагрузка", СтруктураОбмена);
	
КонецПроцедуры



