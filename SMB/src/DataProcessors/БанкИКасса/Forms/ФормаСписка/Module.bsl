
&НаКлиенте
&После("БанковскиеВыпискиПриАктивизацииСтроки")
Процедура СМП_БанковскиеВыпискиПриАктивизацииСтроки(Элемент)
	
	// СМП_РаботаСБанками +++
	СМП_ОпределитьДоступностьЗагрузитьИзФайла();
	// СМП_РаботаСБанками ---
	
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ВыгрузитьВКлиентБанк")
Процедура СМП_ВыгрузитьВКлиентБанк(Команда)

	ПараметрыОткрытия = Новый Структура("НачПериода, КонПериода, БанковскийСчетОрганизации", 
	ОтборПериодПП.ДатаНачала, ОтборПериодПП.ДатаОкончания, );
	ТекущиеДанные = Элементы.СписокПлатежныхПоручений.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.БанковскийСчет) Тогда
		БанковскийСчетОрганизации = ТекущиеДанные.БанковскийСчет;
		ПараметрыОткрытия.Вставить("БанковскийСчетОрганизации", БанковскийСчетОрганизации);
	КонецЕсли;
	Если ТекущиеДанные <> Неопределено И ОтборПериодПП.ДатаНачала = '00010101' И ОтборПериодПП.ДатаОкончания = '00010101' Тогда
		ПараметрыОткрытия.Вставить("НачПериода", НачалоДня(ТекущиеДанные.Дата));
		ПараметрыОткрытия.Вставить("КонПериода", КонецДня(ТекущиеДанные.Дата));
	КонецЕсли;

	#Удаление
	ПараметрыОткрытия.Вставить("РежимПоУмолчанию", "Выгрузки");
	#КонецУдаления

	ОткрытьФорму(
	
	#Удаление
	"Обработка.КлиентБанк.Форма.Форма", 
	#КонецУдаления
	
	#Вставка
	// СМП_РаботаСБанками +++
	"Обработка.СМП_КлиентБанк.Форма.ФормаВыгрузка", 
	// СМП_РаботаСБанками ---
	#КонецВставки
	
	ПараметрыОткрытия);

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ЗагрузитьИзФайла")
Процедура СМП_ЗагрузитьИзФайла(Команда)

	#Удаление
	ЗагрузитьИзФайлаФрагмент();
	#КонецУдаления
	
	#Вставка
	// СМП_РаботаСБанками +++
	РС = Неопределено;
	Эл = ЭтаФорма.Элементы.БанковскиеВыписки.ТекущиеДанные;
	
	Если Эл <> Неопределено Тогда
		РС = Эл.БанковскийСчет;
	Иначе
		
		мОтбор = Новый Структура("ИмяГруппыРодителя, ИмяПараметраЗапроса, ИмяПоляОтбора, ИмяСписка", 
		"ГруппаОтборСчетИКасса", "СписокКассИСчетов", "СчетКассаДляОтбора", "БанковскиеВыписки");
		СтрокиОтбора = ДанныеМеток.НайтиСтроки(мОтбор);
		Для Каждого мСтрока Из СтрокиОтбора Цикл
			Если ТипЗнч(мСтрока.Метка) = тип("СправочникСсылка.БанковскиеСчета") Тогда
				РС = мСтрока.Метка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ЗагрузитьИзФайлаФрагмент(РС);
	// СМП_РаботаСБанками ---
	#КонецВставки

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ЗагрузитьИзФайлаФрагмент")
Процедура СМП_ЗагрузитьИзФайлаФрагмент(ПараметрБанковскийСчет)

	Если ПараметрБанковскийСчет = Неопределено Тогда
		СтруктураПрямогоОбмена = КлиентБанкВызовСервера.ПолучитьСтруктуруПрямогоОбмена();
		Если СтруктураПрямогоОбмена.БанковскийСчет = Неопределено Тогда // Есть несколько счетов, нужно выбирать.
			Отбор = Новый Структура("ЭтоСчетОрганизации", Истина);
			ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаВыбораБезВладельца", Новый Структура("ТекущаяСтрока, Отбор", СтруктураПрямогоОбмена.БанковскийСчет, Отбор), ЭтотОбъект);
		Иначе // Один счет, можно сразу подставить его.
			ОткрытьФорму(
			
			#Удаление
			"Обработка.КлиентБанк.Форма.Форма",
			#КонецУдаления
			
			#Вставка
			// СМП_РаботаСБанками +++
			"Обработка.СМП_КлиентБанк.Форма.ФормаЗагрузка",
			// СМП_РаботаСБанками ---
			#КонецВставки
			
			СтруктураПрямогоОбмена);
		КонецЕсли;
	Иначе
		СтруктураОбмена = КлиентБанкВызовСервера.ПолучитьСтруктуруОбмена(ПараметрБанковскийСчет);
		ОткрытьФорму(
		
		#Удаление
		"Обработка.КлиентБанк.Форма.Форма",
		#КонецУдаления
		
		#Вставка
		// СМП_РаботаСБанками +++
		"Обработка.СМП_КлиентБанк.Форма.ФормаЗагрузка",
		// СМП_РаботаСБанками ---
		#КонецВставки
		
		СтруктураОбмена);
	КонецЕсли;

КонецПроцедуры


// Определяет доступность команды загрузки банковской выписки согласно данным в текущей строке списка.
//
&НаКлиенте
Процедура СМП_ОпределитьДоступностьЗагрузитьИзФайла()
	
	Если Элементы.ЗагрузитьИзФайла.Видимость Тогда
		
		ТекДанные = Элементы.БанковскиеВыписки.ТекущиеДанные;
		
		Если ТекДанные <> Неопределено Тогда
			Элементы.ЗагрузитьИзФайла.Доступность = (ТипЗнч(ТекДанные.СчетКасса) = Тип("СправочникСсылка.БанковскиеСчета"))
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры //СМП_ОпределитьДоступностьЗагрузитьИзФайла

